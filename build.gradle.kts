@file:Suppress("UNUSED_VARIABLE")

import com.android.build.gradle.AppExtension
import com.android.build.gradle.BaseExtension
import groovy.json.JsonOutput
import java.net.URL
import java.util.*
import org.jetbrains.kotlin.compose.compiler.gradle.ComposeCompilerGradlePluginExtension

// Плагин сборки Go и kaidl (Kr328) есть только на изменяемой ветке чужого зеркала
// без контрольных сумм, поэтому лежат в дереве (maven/, байт в байт с зеркала) и
// ищутся только там.
buildscript {
    repositories {
        exclusiveContent {
            forRepository { maven(rootProject.projectDir.resolve("maven")) }
            filter { includeGroupByRegex("com\\.github\\.kr328\\..*") }
        }
        mavenCentral()
        google()
    }
    dependencies {
        classpath(libs.build.r8)
        classpath(libs.build.android)
        classpath(libs.build.kotlin.common)
        classpath(libs.build.kotlin.serialization)
        classpath(libs.build.kotlin.compose)
        classpath(libs.build.ksp)
        classpath(libs.build.golang)
    }
}

subprojects {
    repositories {
        exclusiveContent {
            forRepository { maven(rootProject.projectDir.resolve("maven")) }
            filter { includeGroupByRegex("com\\.github\\.kr328\\..*") }
        }
        mavenCentral()
        google()
    }

    val isApp = name == "app"

    val abiList: List<String> = (project.findProperty("clod.abi") as String?)
        ?.split(",")?.map(String::trim)?.filter(String::isNotEmpty)
        ?: listOf("arm64-v8a", "armeabi-v7a", "x86_64")

    apply(plugin = if (isApp) "com.android.application" else "com.android.library")

    fun queryConfigProperty(key: String): Any? {
        val localProperties = Properties()
        val localPropertiesFile = rootProject.file("local.properties")
        if (localPropertiesFile.exists()) {
            localProperties.load(localPropertiesFile.inputStream())
        } else {
            return null
        }
        return localProperties.getProperty(key)
    }

    extensions.configure<BaseExtension> {
        buildFeatures.buildConfig = true
        defaultConfig {
            if (isApp) {
                val customApplicationId = queryConfigProperty("custom.application.id") as? String?
                applicationId = customApplicationId.takeIf { it?.isNotBlank() == true } ?: "io.clodclash.app"
            }

            project.name.let { name ->
                namespace = if (name == "app") "com.github.kr328.clash"
                else "com.github.kr328.clash.$name"
            }

            minSdk = 23
            targetSdk = 35

            versionName = "0.1.22-alpha.8"
            versionCode = 12208

            resValue("string", "release_name", "v$versionName")
            resValue("integer", "release_code", "$versionCode")

            ndk {
                abiFilters += abiList
            }

            externalNativeBuild {
                cmake {
                    abiFilters(*abiList.toTypedArray())
                }
            }

            if (!isApp) {
                consumerProguardFiles("consumer-rules.pro")
            } else {
                setProperty("archivesBaseName", "clodclash-$versionName")
            }
        }

        ndkVersion = "29.0.14206865"

        compileSdkVersion(defaultConfig.targetSdk!!)

        if (isApp) {
            packagingOptions {
                resources {
                    excludes.add("DebugProbesKt.bin")
                }
            }
        }

        productFlavors {
            flavorDimensions("feature")

            create("standard") {
                isDefault = true
                dimension = flavorDimensionList[0]

                resValue("string", "launch_name", "Clod Clash")
                resValue("string", "application_name", "Clod Clash")
            }
        }

        signingConfigs {
            val keystore = rootProject.file("signing.properties")
            if (keystore.exists()) {
                create("release") {
                    val prop = Properties().apply {
                        keystore.inputStream().use(this::load)
                    }

                    storeFile = rootProject.file(prop.getProperty("keystore.path") ?: "release.keystore")
                    storePassword = prop.getProperty("keystore.password")!!
                    keyAlias = prop.getProperty("key.alias")!!
                    keyPassword = prop.getProperty("key.password")!!
                }
            }
        }

        buildTypes {
            named("release") {
                isMinifyEnabled = isApp
                isShrinkResources = isApp
                signingConfig = signingConfigs.findByName("release")
                proguardFiles(
                    getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"
                )
            }
            named("debug") {
                versionNameSuffix = ".debug"
            }
        }

        if (isApp) {
            this as AppExtension

            splits {
                abi {
                    isEnable = true
                    isUniversalApk = true
                    reset()
                    include(*abiList.toTypedArray())
                }
            }
        }

        compileOptions {
            sourceCompatibility = JavaVersion.VERSION_21
            targetCompatibility = JavaVersion.VERSION_21
        }
    }

    val composeReports: Boolean = project.findProperty("clod.composeReports")?.toString()
        ?.let { it.isBlank() || it.toBoolean() } == true

    if (composeReports) {
        plugins.withId("org.jetbrains.kotlin.plugin.compose") {
            val destination = layout.buildDirectory.dir("compose-reports")

            extensions.configure<ComposeCompilerGradlePluginExtension> {
                metricsDestination.set(destination)
                reportsDestination.set(destination)
            }
        }
    }
}

task("clean", type = Delete::class) {
    delete(rootProject.buildDir)
}

tasks.wrapper {
    distributionType = Wrapper.DistributionType.BIN

    doLast {
        val sha256 = URL("$distributionUrl.sha256").openStream()
            .use { it.reader().readText().trim() }

        file("gradle/wrapper/gradle-wrapper.properties")
            .appendText("\ndistributionSha256Sum=$sha256\n")
    }
}
